!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself. Token names are identified by all CAPITAL
    letters and rule names are identified by lowerCamelCase letters.

    The INTRINSIC tokens are environment dependent and therefore left undefined.
    The tokens are scanned in the order listed so an INTRINSIC token takes
    precedence over an IDENTIFIER token.

    The rules are applied in the order listed as well, so within a factor a
    range takes precedence over an individual CHARACTER.  The starting rule is
    the "$grammar" rule.
<!

$grammar: <statement> EOF  ! Terminated by an end-of-file marker.

$statement: (COMMENT | production) <EOL>

$production: SYMBOL ":" rule

$rule: [EOL] alternative {[EOL] "|" alternative}

$alternative: <factor> [NOTE]

$factor:
      range  ! A range of characters.
    | "~" factor  ! The inversion of the factor.
    | "[" rule "]"  ! Zero or one instances of the rule.
    | "(" rule ")" count  ! Exact (default one) number of instances of the rule.
    | "<" rule ">" count  ! Minimum (default one) number of instances of the rule.
    | "{" rule "}" count  ! Maximum (default unlimited) number of instances of the rule.
    | CHARACTER
    | LITERAL
    | INTRINSIC
    | IDENTIFIER

$range: CHARACTER ".." CHARACTER

$count: {DIGIT}  ! No digits signifies the default count.

$CHARACTER: "'" ~"'" "'"

$LITERAL: '"' <~'"'> '"'

$INTRINSIC: "LETTER" | "DIGIT" | "EOL" | "EOF"

$SYMBOL: "$" IDENTIFIER

$IDENTIFIER: LETTER {LETTER | DIGIT}

$NOTE: "! " {~EOL}

$COMMENT: "!>" EOL {COMMENT | ~"<!"} EOL "<!"

