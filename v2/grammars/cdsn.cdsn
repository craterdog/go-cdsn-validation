!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself. Token names are identified by all CAPITAL
    letters and rule names are identified by lowerCamelCase letters.

    The INTRINSIC tokens are environment dependent and therefore left undefined.
    The tokens are scanned in the order listed so an INTRINSIC token takes
    precedence over an IDENTIFIER token.

    The rules are applied in the order listed as well, so within a factor a
    range takes precedence over an individual CHARACTER.  The starting rule is
    the "$source" rule.
<!

$CHARACTER: "'" ~"'" "'"

$COMMENT: "!>" EOL {COMMENT | ~"<!"} EOL "<!"

$IDENTIFIER: LETTER {LETTER | DIGIT}

$INTRINSIC: "LETTER"
    | "DIGIT"
    | "EOL"
    | "EOF"

$LITERAL: '"' <~'"'> '"'

$NOTE: "! " {~EOL}

$SYMBOL: "$" IDENTIFIER

$factor: range
    | "~" factor
    | "(" rule ")"
    | "[" rule "]"
    | "{" rule "}"
    | "<" rule ">"
    | CHARACTER
    | LITERAL
    | INTRINSIC
    | IDENTIFIER

$option: <factor> [[NOTE] EOL]

$production: SYMBOL ":" rule

$range: CHARACTER ".." CHARACTER

$rule: option {"|" option}

$source: <statement> EOF

$statement: (COMMENT | production) <EOL>

