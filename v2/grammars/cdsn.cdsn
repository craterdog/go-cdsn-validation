!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself. Token names are identified by all CAPITAL
    letters and rule names are identified by lowerCamelCase letters.

    The INTRINSIC tokens are environment dependent and therefore left undefined.
    The tokens are scanned in the order listed so an INTRINSIC token takes
    precedence over an IDENTIFIER token.

    The rules are applied in the order listed as well, so within a factor a
    range takes precedence over an individual CHARACTER.  The starting rule is
    the "$grammar" rule.
<!

$grammar: <statement> EOF  ! Terminated by an end-of-file marker.

$statement: (COMMENT | production) <EOL>

$production: SYMBOL ":" rule

$rule: [EOL] option {[EOL] "|" option}

$option: <factor> [NOTE]

$factor:
      range  ! A range of characters.
    | "~" factor  ! The inversion of the factor.
    | "(" rule ")" count  ! Exactly count instances of the rule.
    | "[" rule "]" count  ! Zero or count (default is one) instances of the rule.
    | "{" rule "}" count  ! Zero or more up to count (default is infinity) instances of the rule.
    | "<" rule ">" count  ! One or more up to count (default is infinity) instances of the rule.
    | CHARACTER
    | LITERAL
    | INTRINSIC
    | IDENTIFIER

$range: CHARACTER ".." CHARACTER

$count: {DIGIT}  ! No digits means a default count of one.

$CHARACTER: "'" ~"'" "'"

$LITERAL: '"' <~'"'> '"'

$INTRINSIC: "LETTER" | "DIGIT" | "EOL" | "EOF"

$SYMBOL: "$" IDENTIFIER

$IDENTIFIER: LETTER {LETTER | DIGIT}

$NOTE: "! " {~EOL}

$COMMENT: "!>" EOL {COMMENT | ~"<!"} EOL "<!"

