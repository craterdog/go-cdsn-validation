
!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself.  This language grammar consists of rule definitions
    and token definitions.

    Each rule name begins with a lowercase letter.  The rules are applied in the
    order listed. So—for example—within a factor, a range of RUNEs takes
    precedence over an individual RUNE.

    Each token name begins with an uppercase letter.  The INTRINSIC tokens are
    environment and language specific, and are therefore left undefined. The
    tokens are also scanned in the order listed.  So an INTRINSIC token takes
    precedence over an IDENTIFIER token.
<!


! *** Rule Names Begin with Lowercase and May Contain Whitespace ***

$grammar: <statement> EOF  ! Terminated with an end-of-file marker.

$statement: annotation | production

$annotation: NOTE | COMMENT

$production: SYMBOL ":" definition  ! This works for both tokens and rules.

$definition: alternative {"|" alternative}

$alternative: <factor> [NOTE]

$factor:
      inverse
    | exactlyN
    | zeroOrOne
    | zeroOrMore
    | oneOrMore
    | range
    | token  ! Must be checked last.

$inverse: "~" factor

$exactlyN: "(" definition ")" [NUMBER]  ! The default is exactly one.

$zeroOrOne: "[" definition "]"

$zeroOrMore: "{" definition "}"

$oneOrMore: "<" definition ">"

$range: RUNE ".." RUNE  ! A range includes the first and last RUNEs listed.

$token: RUNE | STRING | NUMBER | NAME | INTRINSIC


! *** Token Names Begin with Uppercase and Cannot Reference Rules ***

$INTRINSIC: "LOWERCASE" | "UPPERCASE" | "DIGIT" | "EOL" | "EOF"

$IGNORE: " " | EOL

$NOTE: "! " {~EOL}

$COMMENT: "!>" EOL {COMMENT | ~"<!"} EOL "<!"

$RUNE: "'" ~"'" "'"

$STRING: '"' <~'"'> '"'

$NUMBER: <DIGIT>

$LETTER: LOWERCASE | UPPERCASE

$CHARACTER: LETTER | DIGIT

$NAME: LETTER {CHARACTER}  ! NAMEs must be scanned after INTRINSICs.

$SYMBOL: "$" NAME

$DELIMITER: "~" | ":" | "|" | "(" | ")" | "[" | "]" | "{" | "}" | "<" | ">" | ".."

