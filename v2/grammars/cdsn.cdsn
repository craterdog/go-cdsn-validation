
!>
    CRATER DOG SYNTAX NOTATION
    This document contains a formal definition of the Crater Dog Syntax Notationâ„¢ (CDSN) using CDSN
    itself in homage to Douglas Hofstadter.  A language grammar consists of a set of token and rule
    definitions.
<!


!>
    TOKEN DEFINITIONS
    The following token definitions are used by the scanner to generate the stream of tokens that
    are processed by the parser.  Each token name begins with an upper case letter.  Unlike with
    rule definitions, a token definition cannot specify the name of a rule but can specify the
    name of another token.  Token definitions cannot be recursive and the scanning of tokens is
    not greedy.  Whitespace within a token definition is not ignored.  The following intrinsic
    token types are environment/language specific:
     * ANY - any character
     * LOWER_CASE - any language specific lower case character
     * UPPER_CASE - any language specific upper case character
     * DIGIT - any language specific digit
     * SEPARATOR - any language specific character separator
     * ESCAPE - any environment specific escape sequence
     * EOL - the environment specific end-of-line sequence
     * EOF - the environment specific end-of-file marker
    The following token types are scanned for in the order listed below.
<!

$INTRINSIC: "ANY" | "LOWER_CASE" | "UPPER_CASE" | "DIGIT" | "SEPARATOR" | "ESCAPE" | "EOL" | "EOF"

$NOTE: "! " *(~EOL)

$COMMENT: "!>" *ANY "<!"

$CHARACTER: "'" ~EOL "'"

$STRING: '"' *(ESCAPE | ~('"' | EOL)) '"'

$NAME: (LOWER_CASE | UPPER_CASE) *(?SEPARATOR (LOWER_CASE | UPPER_CASE | DIGIT))

$SYMBOL: "$" NAME

$CONSTRAINT: "~" | "?" | "*" | "+" | +DIGIT


!>
    RULE DEFINITIONS
    The following rules are used by the parser when parsing the stream of tokens generated by the
    scanner.  Each rule name begins with a lower case letter.  The rule definitions may specify the
    names of tokens or other rules and are matched by the parser in the order listed.  Rule
    definitions may be recursive.  The sequence of predicates within in a rule definition may be
    separated by whitespace which is ignored by the parser.  The factors within the predicates may
    be limited by one of the following constraints:
     * ~factor - Any factor EXCEPT the specified factor
     * ?factor - Zero or one instances of the specified factor
     * *factor - Zero or more instances of the specified factor
     * +factor - One or more instances of the specified factor
     * Nfactor - Exactly N instances of the specified factor
    The following rules are attempted in the order listed below.
<!

$document: +statement EOF  ! Terminated with an end-of-file marker.

$statement: definition | COMMENT

$definition: SYMBOL ":" expression  ! This works for both tokens and rules.

$expression: alternative *("|" alternative)

$alternative: +predicate ?NOTE

$predicate: range | repetition | factor

$range: CHARACTER ?(".." CHARACTER)  ! A range of CHARACTERs is inclusive.

$repetition: CONSTRAINT factor

$factor: precedence | element

$precedence: "(" expression ")"

$element: INTRINSIC | NAME | STRING

