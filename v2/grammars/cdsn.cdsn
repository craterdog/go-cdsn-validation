!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself.  This language grammar consists of rule definitions
	and token definitions.

    Each rule name begins with a lowercase letter.  The rules are applied in the
	order listed. So—for example—within a factor, a range of RUNEs takes
	precedence over an individual RUNE.  The starting rule is the "$grammar" rule.

	Each token name begins with an uppercase letter.  The INTRINSIC tokens are
	environment and language specific, and are therefore left undefined. The
	tokens are also scanned in the order listed.  So an INTRINSIC token takes
	precedence over an IDENTIFIER token.
<!

! *** Rules Begin with a Lowercase Letter ***

$grammar: <statement> EOF  ! Terminated by an end-of-file marker.

$statement: (annotation | production) <EOL>

$annotation: NOTE | COMMENT

$production: SYMBOL ":" definition

$definition: [EOL] alternative {[EOL] "|" alternative}

$alternative: <factor> [NOTE]

$factor:
      range  ! A range of runes.
    | "~" factor  ! The inversion of the factor.
    | "[" definition "]"  ! Zero or one instances of the definition.
    | "(" definition ")" [NUMBER]  ! Exact (default one) number of instances of the definition.
    | "<" definition ">" [NUMBER]  ! Minimum (default one) number of instances of the definition.
    | "{" definition "}" [NUMBER]  ! Maximum (default unlimited) number of instances of the definition.
    | RUNE
    | STRING
    | NUMBER
    | INTRINSIC
    | IDENTIFIER
    | DELIMITER

$range: RUNE ".." RUNE  ! A range includes the first and last RUNEs listed.

! *** Tokens Begin with an Uppercase Letter ***

$NOTE: "! " {~EOL}

$COMMENT: "!>" EOL {COMMENT | ~"<!"} EOL "<!"

$RUNE: "'" ~"'" "'"

$STRING: '"' <~'"'> '"'

$NUMBER: <DIGIT>

$INTRINSIC: "LOWERCASE" | "UPPERCASE" | "DIGIT" | "EOL" | "EOF"

$IDENTIFIER: RULE | TOKEN

$SYMBOL: "$" IDENTIFIER

$RULE: LOWERCASE {CHARACTER}

$TOKEN: UPPERCASE {CHARACTER}

$LETTER: LOWERCASE | UPPERCASE

$CHARACTER: LETTER | DIGIT

$DELIMITER: "~" | ":" | "|" | "(" | ")" | "[" | "]" | "{" | "}" | "<" | ">" | ".."

