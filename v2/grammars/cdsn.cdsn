!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself.  This language grammar consists of rule definitions
    and token definitions.

    Each rule name begins with a lowercase letter.  The rules are applied in the
    order listed. So—for example—within a factor, a range of RUNEs takes
    precedence over an individual RUNE.

    Each token name begins with an uppercase letter.  The INTRINSIC tokens are
    environment and language specific, and are therefore left undefined. The
    tokens are also scanned in the order listed.  So an INTRINSIC token takes
    precedence over an IDENTIFIER token.
<!

! *** Rules Begin with a Lowercase Letter ***

$grammar: <statement> EOF  ! Terminated with an end-of-file marker.

$statement: (annotation | production) <EOL>

$annotation: NOTE | COMMENT

$production: SYMBOL ":" definition  ! This works for both tokens and rules.

$definition: [EOL] alternative {[EOL] "|" alternative}

$alternative: <factor> [NOTE]

$factor:
      inverse
    | zeroOrOne
    | zeroOrMore
    | oneOrMore
    | exactlyN
    | range  ! A range must be checked before a RUNE.
    | RUNE
    | STRING
    | NUMBER
    | INTRINSIC
    | TOKEN
    | RULE
    | DELIMITER

$range: RUNE ".." RUNE  ! A range includes the first and last RUNEs listed.

$inverse: "~" factor

$zeroOrOne: "[" definition "]"

$zeroOrMore: "{" definition "}"

$oneOrMore: "<" definition ">"

$exactlyN: "(" definition ")" [NUMBER]  ! The default is exactly one.

! *** Tokens Begin with an Uppercase Letter ***

$INTRINSIC: "LOWERCASE" | "UPPERCASE" | "DIGIT" | "EOL" | "EOF"

$NOTE: "! " {~EOL}

$COMMENT: "!>" EOL {COMMENT | ~"<!"} EOL "<!"

$SYMBOL: "$" LETTER {CHARACTER}

$LETTER: LOWERCASE | UPPERCASE

$CHARACTER: LETTER | DIGIT

$RUNE: "'" ~"'" "'"

$STRING: '"' <~'"'> '"'

$NUMBER: <DIGIT>

$TOKEN: UPPERCASE {CHARACTER}  ! Token names must be scanned after intrinsics.

$RULE: LOWERCASE {CHARACTER}  ! Rule names must be scanned after intrinsics.

$DELIMITER: "~" | ":" | "|" | "(" | ")" | "[" | "]" | "{" | "}" | "<" | ">" | ".."

