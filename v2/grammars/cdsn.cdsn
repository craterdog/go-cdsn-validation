
!>
    CRATER DOG SYNTAX NOTATION
    This document contains a formal definition of the Crater Dog Syntax Notationâ„¢
    (CDSN) language grammar using CDSN itself.  This grammar consists of a set of
    rule definitions and token definitions.
<!


!>
    RULE DEFINITIONS
    The following rules are used by the parser when parsing the stream of tokens
    generated by the scanner.  Each rule name begins with a lowercase letter.
    The rule definitions may reference tokens or other rules and are attempted
    by the parser in the order listed.  The factors within in a rule definition
    may be separated by whitespace which is ignored by the parser.
<!

$grammar: <statement> EOF  ! Terminated with an end-of-file marker.

$statement: COMMENT | production

$production: SYMBOL ":" definition  ! This works for both tokens and rules.

$definition: alternative {"|" alternative}

$alternative: <factor> [NOTE]

$factor:
      inverse
    | exactlyN
    | zeroOrOne
    | zeroOrMore
    | oneOrMore
    | range
    | token  ! Must be checked for last.

$inverse: "~" factor

$exactlyN: "(" definition ")" [NUMBER]  ! The default is exactly one.

$zeroOrOne: "[" definition "]"

$zeroOrMore: "{" definition "}"

$oneOrMore: "<" definition ">"

$range: RUNE ".." RUNE  ! A range includes the first and last RUNEs listed.

$token: INTRINSIC | RUNE | STRING | NUMBER | NAME


!>
    TOKEN DEFINITIONS
    The following token definitions are used by the scanner to generate the
    stream of tokens that are processed by the parser.  Each token name begins
    with an uppercase letter.  Unlike with rule definitions, a token definition
    cannot reference a rule and the whitespace within a token definition is not
    ignored.  The INTRINSIC tokens are environment/language specific, and are
    therefore left undefined. The token definitions are processed in the order
    listed.
<!

$INTRINSIC: "LOWERCASE" | "UPPERCASE" | "DIGIT" | "EOL" | "EOF"

$LETTER: LOWERCASE | UPPERCASE

$CHARACTER: LETTER | DIGIT

$IGNORED: " " | EOL

$NOTE: "! " {~EOL}

$COMMENT: "!>" EOL {COMMENT | ~"<!"} EOL "<!"

$RUNE: "'" ~"'" "'"

$STRING: '"' <~'"'> '"'

$NUMBER: <DIGIT>

$NAME: LETTER {CHARACTER}  ! NAMEs must be scanned after INTRINSICs.

$SYMBOL: "$" NAME

$DELIMITER: "~" | ":" | "|" | "(" | ")" | "[" | "]" | "{" | "}" | "<" | ">" | ".."

