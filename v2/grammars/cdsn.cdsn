
!>
    CRATER DOG SYNTAX NOTATION
    This document contains a formal definition of the Crater Dog Syntax Notationâ„¢ (CDSN) using CDSN
    itself in homage to Douglas Hofstadter.  A language grammar consists of a set of token and rule
    definitions.
<!


!>
    TOKEN DEFINITIONS
    The following token definitions are used by the scanner to generate the stream of tokens that
    are processed by the parser.  Each token name begins with an upper case letter.  Unlike with
    rule definitions, a token definition cannot specify the name of a rule but can specify the
    name of another token.  Token definitions cannot be recursive and the scanning of tokens is
    not greedy.  Whitespace within a token definition is not ignored.

    The following intrinsic token types are environment or language specific:
     * ANY - any language specific character
     * LOWER_CASE - any language specific lower case character
     * UPPER_CASE - any language specific upper case character
     * DIGIT - any language specific digit
     * SEPARATOR - any language specific character separator
     * ESCAPE - any environment specific escape sequence
     * CONTROL - any environment specific (non-printable) control character
     * EOF - the environment specific end-of-file marker

    Inversion within a token definition may only be applied to a token factor resulting in a single
    character.  Token inversions cannot be nested.  The following token types are scanned for in
    the order listed below.
<!

$INTRINSIC: "ANY" | "LOWER_CASE" | "UPPER_CASE" | "DIGIT" | "SEPARATOR" | "ESCAPE" | "CONTROL" | "EOF"

$NOTE: "! " (~CONTROL)*

$COMMENT: "!>" ANY* "<!"

$NUMBER: DIGIT+

$CHARACTER: "'" ~CONTROL "'"  ! Any printable character.

$LITERAL: '"' (ESCAPE | ~('"' | CONTROL))+ '"'

$NAME: (LOWER_CASE | UPPER_CASE) (SEPARATOR? (LOWER_CASE | UPPER_CASE | DIGIT))*

$SYMBOL: '$' NAME


!>
    RULE DEFINITIONS
    The following rules are used by the parser when parsing the stream of tokens generated by the
    scanner.  Each rule name begins with a lower case letter.  The rule definitions may specify the
    names of tokens or other rules and are matched by the parser in the order listed.  A rule
    definition may also be directly or indirectly recursive.  The sequence of predicates within
    in a rule definition may be separated by whitespace which is ignored by the parser.

    A factor within a predicate may also be constrained by any of the following cardinalities:
     * factor{M} - Exactly M instances of the specified factor.
     * factor{M..} - M or more instances of the specified factor.
     * factor{M..N} - M to N instances of the specified factor.
     * factor? - Zero or one instances of the specified factor (equivalent to factor{0..1}).
     * factor* - Zero or more instances of the specified factor (equivalent to factor{0..}).
     * factor+ - One or more instances of the specified factor (equivalent to factor{1..}).

    Inversion within a rule definition may only be applied to a token factor resulting in a single
    token.  Rule inversions cannot be nested.  The following rules are attempted in the order
    listed below.
<!

$document: statement+ EOF  ! Terminated with an end-of-file marker.

$statement: definition | COMMENT

$definition: SYMBOL ":" expression  ! This works for both tokens and rules.

$expression: alternative ("|" alternative)*

$alternative: predicate+ NOTE?

$predicate: factor cardinality?  ! The default cardinality is one.

$factor: glyph | element | precedence | inversion

$glyph: CHARACTER (".." CHARACTER)?  ! The range of CHARACTERs in a glyph is inclusive.

$element: INTRINSIC | NAME | LITERAL

$precedence: "(" expression ")"

$inversion: "~" factor

$cardinality:
      "?"  ! Zero or one instance of a factor.
    | "*"  ! Zero or more instances of a factor.
    | "+"  ! One or more instances of a factor.
    | "{" NUMBER (".." NUMBER?)? "}"  ! The range of NUMBERs in a cardinality is inclusive.

