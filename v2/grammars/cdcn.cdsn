
!>
    CRATER DOG COLLECTION NOTATION
    This document is a formal definition of Crater Dog Collection Notation™ (CDCN)
    using Crater Dog Syntax Notation™ (CDSN).
<!


!>
    TOKEN DEFINITIONS
    The following token definitions are used by the scanner to generate the stream of tokens that
    are processed by the parser.  Each token name begins with an upper case letter.  Unlike with
    rule definitions, a token definition cannot specify the name of a rule but can specify the
    name of another token.  Token definitions cannot be recursive and the scanning of tokens is
    not greedy.  Whitespace within a token definition is not ignored.  The following intrinsic
    token types are environment/language specific, and are not defined in this specification:
     * ANY - any allowed character
     * LOWER_CASE - any allowed lower case character
     * UPPER_CASE - any allowed upper case character
     * DIGIT - any allowed digit
     * SEPARATOR - any allowed character separator
     * ESCAPE - any allowed escape sequence
     * EOL - the environment specific end-of-line sequence
     * EOF - the environment specific end-of-file marker
    The token types are scanned for in the order listed in the specification.
<!

$BASE10: '0'..'9'

$BASE16: '0'..'9' | 'a'..'f'

$BOOLEAN: "false" | "true"

$COMPLEX: "(" FLOAT SIGN FLOAT "i)"

$CONTEXT: "array" | "catalog" | "list" | "map" | "queue" | "set" | "stack"

$DELIMITER: "]" | "[" | ")" | "(" | ":" | ","

$EXPONENT: ("e" | "E") SIGN ORDINAL

$FLOAT: [SIGN] SCALAR [EXPONENT]

$FRACTION: "." <BASE10>

$IGNORED: " "

$INTEGER: ZERO | [SIGN] ORDINAL

$LETTER: LOWER_CASE | UPPER_CASE

$NIL: "nil"

$ORDINAL: '1'..'9' {'0'..'9'}

$RUNE: "'" (ESCAPE | ~("'" | EOL)) "'"

$SCALAR: (ZERO | ORDINAL) FRACTION

$SIGN: "+" | "-"

$STRING: '"' {ESCAPE | ~('"' | EOL)} '"'

$UNICODE: "u" (BASE16)4 | "U" (BASE16)8

$UNSIGNED: "0x" <BASE16>

$ZERO: "0"


!>
    RULE DEFINITIONS
    The following rules are used by the parser when parsing the stream of tokens generated by the
    scanner.  Each rule name begins with a lower case letter.  The rule definitions may specify the
    names of tokens or other rules and are matched by the parser in the order listed.  Rule
    definitions may be recursive.  The sequence of factors within in a rule definition may be
    separated by whitespace which is ignored by the parser.  The starting rule is the $source rule.
<!

$association: key ":" value

$associations:
      association {"," association}
    | EOL <association EOL>
    | ":"  ! No associations.

$collection: "[" (values | associations) "]" "(" CONTEXT ")"

$key: primitive

$primitive: BOOLEAN | COMPLEX | FLOAT | INTEGER | NIL | RUNE | STRING

$source: collection EOF  ! EOF is the end-of-file marker.

$value: primitive | collection

$values:
      value {"," value}
    | EOL <value EOL>
    | " "  ! No values.

