!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself. Token names are identified by all CAPITAL
    letters and rule names are identified by lowerCamelCase letters.

    The INTRINSIC tokens are environment dependent and therefore left undefined.
    The tokens are scanned in the order listed so an INTRINSIC token takes
    precedence over an IDENTIFIER token.

    The rules are applied in the order listed as well, so within a factor a
    range takes precedence over an individual CHARACTER.  The starting rule is
    the "$source" rule.
<!

$CHARACTER: "'" ~"'" "'"

$COMMENT: "!>" EOL  {COMMENT | ~"<!"} EOL "<!"

$IDENTIFIER: LETTER {LETTER | DIGIT}

$INTRINSIC: "LETTER" | "DIGIT" | "EOL" | "EOF"

$NOTE: "! " {~EOL}

$STRING: '"' <~'"'> '"'

$SYMBOL: "$" IDENTIFIER

$alternative: [[NOTE] EOL] option

$factor: 
    range        |
    "~" factor   |
    "(" rule ")" |
    "<" rule ">" |
    "[" rule "]" |
    "{" rule "}" |
    CHARACTER    |
    STRING       |
    INTRINSIC    |
    IDENTIFIER

$option: <factor>

$production: SYMBOL ":" rule [NOTE]

$range: CHARACTER ".." CHARACTER

$rule: option {"|" alternative}

$source: <statement> EOF

$statement: (COMMENT | production) <EOL>

