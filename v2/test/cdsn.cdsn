!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself. The token names are identified by all CAPITAL
    characters and the rule names are identified by lowerCamelCase characters.
    The starting rule is "$source".
<!

$CHARACTER: LETTER | DIGIT

$COMMENT: "!>" EOL  {COMMENT | ~"<!"} EOL "<!"  ! Supports nested comments.

$EOL: "\n"  ! Standard POSIX definition.

$IDENTIFIER: LETTER {CHARACTER}

$INTRINSIC: "LETTER" | "DIGIT" | "EOF"

$LITERAL: "'" <~"'"> "'" | '"' <~'"'> '"'

$NOTE: "! " {~EOL}

$RANGE: CHARACTER ".." CHARACTER

$SYMBOL: "$" IDENTIFIER

$alternative: [[NOTE] EOL] option

$factor: 
    RANGE        |  ! A range takes precedence over other factor types.
    INTRINSIC    |  ! These tokens have character set specific definitions.
    IDENTIFIER   |  ! These tokens map to symbols defined in other statements.
    LITERAL      |  ! Represents a literal string in quotes.
    "~" factor   |  ! Indicates the inverse of the factor.
    "(" rule ")" |  ! Indicates that the rule is evaluated first.
    "[" rule "]" |  ! Indicates zero or one repetitions of the rule.
    "{" rule "}" |  ! Indicates zero or more repetitions of the rule.
    "<" rule ">"    ! Indicates one or more repetitions of the rule.

$option: <factor>

$production: SYMBOL ":" rule [NOTE]

$rule: option {"|" alternative}

$source: <statement> EOF  ! EOF is the end-of-file marker.

$statement: (COMMENT | production) <EOL>  ! EOL is the end-of-line character.

