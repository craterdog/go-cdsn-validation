!>
    A formal definition of Crater Dog Syntax Notation™ (CDSN) using Crater Dog
    Syntax Notation™ itself. The token names are identified by all CAPITAL
    characters and the rule names are identified by lowerCamelCase characters.
    The starting rule is "$grammar".
<!

$COMMENT: "!>" EOL  {COMMENT | ~"<!"} EOL "<!"  ! Supports nested comments.

$EOL: "\n"  ! Standard POSIX definition.

$IDENTIFIER: LETTER {LETTER | DIGIT}

$INTRINSIC: "LETTER" | "DIGIT" | "EOF"  ! Language specific definitions.

$NOTE: "! " {~EOL}

$SYMBOL: "$" IDENTIFIER

$alternative: [[NOTE] EOL] option

$factor: 
    INTRINSIC              |
    IDENTIFIER             |
    literal [".." literal] |
    "~" factor             |  ! Indicates the inverse of the factor.
    "(" rule ")"           |  ! Indicates that the rule is evaluated first.
    "[' rule "]"           |  ! Indicates zero or one repetitions of the rule.
    "{" rule "}"           |  ! Indicates zero or more repetitions of the rule.
    "<' rule ">"              ! Indicates one or more repetitions of the rule.

$grammar: <statement> EOF  ! EOF is the end-of-file marker.

$literal: "'" <~"'"> "'" | '"' <~'"'> '"'

$option: <factor>

$production: SYMBOL ":" rule [NOTE]

$rule: option {"|" alternative}

$statement: (COMMENT | production) <EOL>

